$Better training mode (PAL, Rev1) [Feder]
C0000000 0000001F
3D808058 A16C5EE2
3D808000 2C0B0110
40A2000C 39400002
994C01B6 2C0B1100
40A2000C 39400001
994C01B6 3D2080C6
8949F340 2C0A0001
41A200AC 894C01B6
2C0A0000 41A200A0
3CA00000 98A9F22B
3D2080CF 90A950F8
2C0A0001 3D808057
41820044 812CA750
814CA754 816CA758
818CA75C 2C0C0000
4182006C 3CA041B0
90AC05D0 90AB05D0
90AA05D0 3CA04100
90AC05D4 90AB05D4
90AA05D4 90A905D4
48000050 812CA740
814CA744 816CA748
818CA74C 2C0C0000
4182002C 3CA0C1B0
90AC05D0 90AB05D0
90AA05D0 3CA04100
90AC05D4 90AB05D4
90AA05D4 90A905D4
48000010 3D808000
38600000 986C01B6
4E800020 00000000


CODE EXPLAINED:
# This is a C0 code, it runs once per frame

##### PART 1 - BUTTON COMBOS #####
# Check buttons
lis r12, 0x8058			# Load 0x80585ee2, address of controller inputs
lhz r11, 0x5ee2 (r12)

lis r12, 0x8000
cmpwi r11, 0x0110 			# Check if 1, 2 and + all pressed
bne+ DONT_KILL_AWAY
li r10, 2		
stb r10, 0x1b6 (r12)			# SET TRAINING MODE STATUS TO 2 (Kill away)
DONT_KILL_AWAY:

cmpwi r11, 0x1100 			# Check if 1, 2 and - all pressed
bne+ DONT_KILL_HOME
li r10, 1
stb r10, 0x1b6 (r12)			# SET TRAINING MODE STATUS TO 1 (Kill home)
DONT_KILL_HOME:

##### PART 2 - STOP TIMER #####
lis r9, 0x80c6			# Load 0x80c60000 in r9
lbz r10, 0xFFFFf340 (r9)	# Load "isOnline", then Disable training mode if playing online
cmpwi r10, 1
beq+ TRAINING_MODE_NOT_ACTIVE
lbz r10, 0x1b6 (r12)			# LOAD TRAINING MODE STATUS, then diable training mode if it's 0
cmpwi r10, 0
beq+ TRAINING_MODE_NOT_ACTIVE
lis r5, 0			# Load 0x80c5f22b, address with isFirstToX boolean value and Set it to 0 (timed)
stb r5, 0xFFFFf22b (r9)
lis r9, 0x80cf			# Load 0x80cf50f8, address with timer
stw r5, 0x50f8 (r9)			# Set it to 0

##### PART 3 - MAKE OPPONENTS DISAPPEAR #####
cmpwi r10, 1			# Check TRAINING MODE STATUS
lis r12, 0x8057			# Load address for Player objects in r12
beq KILL_HOME_TEAM

### KILL AWAY TEAM ###
lwz r9, 0xFFFFa750 (r12) 	# Captain object
lwz r10, 0xFFFFa754 (r12) # Top sidekick object
lwz r11, 0xFFFFa758 (r12) # Bottom sidekick object
lwz r12, 0xFFFFa75c (r12)	# Back sidekick object
cmpwi r12, 0
beq PLAYERS_ARENT_LOADED
lis r5, 0x41b0			# FLOAT 22 for X Position
stw r5, 0x5d0 (r12) # move back SK to X 22
stw r5, 0x5d0 (r11) # move bottom SK to X 22
stw r5, 0x5d0 (r10) # move top SK to X 22
lis r5, 0x4100			# FLOAT 6 for Y position
stw r5, 0x5d4 (r12) # move back SK to Y 6
stw r5, 0x5d4 (r11) # move bottom SK to Y 6
stw r5, 0x5d4 (r10) # move top SK to Y 6
stw r5, 0x5d4 (r9)  # move captain to Y 6
b END

### KILL HOME TEAM ###
KILL_HOME_TEAM:
lwz r9, 0xFFFFa740 (r12) 	# Captain object
lwz r10, 0xFFFFa744 (r12) # Top sidekick object
lwz r11, 0xFFFFa748 (r12) # Bottom sidekick object
lwz r12, 0xFFFFa74c (r12)	# Back sidekick object
cmpwi r12, 0
beq PLAYERS_ARENT_LOADED
lis r5, 0xc1b0			# FLOAT -22 for X Position
stw r5, 0x5d0 (r12) # move back SK to X -22
stw r5, 0x5d0 (r11) # move bottom SK to X -22
stw r5, 0x5d0 (r10) # move top SK to X -22
lis r5, 0x4100			# FLOAT 6 for Y position
stw r5, 0x5d4 (r12) # move back SK to Y 6
stw r5, 0x5d4 (r11) # move bottom SK to Y 6
stw r5, 0x5d4 (r10) # move top SK to Y 6
stw r5, 0x5d4 (r9)  # move captain to Y 6
b END

PLAYERS_ARENT_LOADED:
TRAINING_MODE_NOT_ACTIVE:
lis r12, 0x8000
li r3, 0
stb r3, 0x1b6 (r12)			# SET TRAINING MODE STATUS TO 0 (Off)

END:
blr	# BLR IS HOW EVERY C0 CODE ENDS